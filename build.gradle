import net.thebugmc.gradle.sonatypepublisher.PublishingType

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.0.20'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id 'maven-publish'
    id "net.thebugmc.gradle.sonatype-central-portal-publisher" version "1.2.4"
}

group = astralisGroup
version = astralisVersion

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "Minecraft Libraries"
        url = "https://libraries.minecraft.net"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "sonatype-oss-snapshots1"
        url = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    api "io.papermc.paper:paper-api:${paperVersion}-R0.1-SNAPSHOT"
    api "org.jetbrains.kotlin:kotlin-stdlib"
}

kotlin {
    jvmToolchain(21)
}

centralPortal {
    username = System.getenv("SONATYPE_USERNAME")
    password = System.getenv("SONATYPE_PASSWORD")

    publishingType = PublishingType.AUTOMATIC

    name = rootProject.name

    pom {
        name = rootProject.name
        url = "https://github.com/building-tools/astralis"
        description = projectDescription

        licenses {
            license {
                name = "Apache 2.0"
                url = "https://github.com/building-tools/astralis/blob/master/LICENSE"
            }
        }

        developers {
            developer {
                id = "raphael-goetz"
                name = "Raphael Goetz"
                organization = "Building-tools"
                organizationUrl = "https://github.com/building-tools"
            }
        }
        scm {
            connection = "scm:git:git://github.com/building-tools/astralis.git"
            developerConnection = "scm:git:git@github.com:building-tools/building-tools.git"
            url = "https://github.com/building-tools/astralis"
            tag = "HEAD"
        }

        ciManagement {
            system = "GitHub Actions"
            url = "https://github.com/building-tools/astralis/actions"
        }
    }
}

signing {
    isRequired = System.getenv("CI") != null

    var privateKey = System.getenv("GPG_PRIVATE_KEY")
    var passphrase = System.getenv("GPG_PRIVATE_PASSWORD")
    useInMemoryPgpKeys(privateKey, passphrase)

    sign(publishing.publications)
}